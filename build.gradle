import groovy.json.JsonSlurper

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'application'
}

/* Get first three version numbers from code.json */
def codeJson = new JsonSlurper().parseText(new File("$projectDir", "code.json").text)
// NOTE: codeJson.version is a String[], and any IDE warning here can be safely ignored
def jsonVersion = codeJson.version[0]

/* Get last version number from commit counts */
def lastTagHash = "git rev-list --tags --max-count=1".execute().text

def commitCount = "NO_TAG_PRESENT"
if (! lastTagHash.isEmpty()){
    lastTagHash = lastTagHash.substring(0, lastTagHash.length() - 1)
    commitCount = ("git rev-list "+lastTagHash+".. --count").execute().text
    commitCount = commitCount.substring(0, commitCount.length() - 1)
}


def version = "$jsonVersion.$commitCount"
mainClassName = 'asl.sensor.SensorSuite'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    api project(path: ':asl-java-utils')

    implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.0'

    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.+'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.+'
    implementation group: 'commons-configuration', name: 'commons-configuration', version: '1.7'

    implementation group: 'net.sf.py4j', name: 'py4j', version: '0.10.7'

    testImplementation group: 'junit', name: 'junit', version: '4.+'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveVersion = version
    archiveBaseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': 'ASL Sensor Test Suite',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }

    /* Adds all dependent libraries*/
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    dependsOn configurations.compileClasspath
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task jarServer(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveVersion = version
    archiveBaseName = 'CalServer'
    manifest {
        attributes 'Implementation-Title': 'Calibration Processing Server(cmd)',
                'Implementation-Version': version,
                'Main-Class': 'asl.sensor.CalProcessingServer'
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    dependsOn configurations.compileClasspath
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}


test {
    maxHeapSize = "4096m"
    maxParallelForks = 4
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Result: ${result.resultType} " +
                        "(${result.testCount} tests, ${result.successfulTestCount} successes, " +
                        "${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

/*Turn off doclint since it is far too strict and breaks the javadoc every time*/
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava {
    options.incremental = true
}

task copyJar(type: Copy) {
    from jar
    into rootDir
}


task copyServerJar(type: Copy) {
    from jarServer
    into rootDir
}

gradle.beforeProject {
    println("Perform pre-project setup")
    def sout = new StringBuilder()
    def serr = new StringBuilder()
    println("Updating gitsubmodules")
    def proc = "git submodule update --init --recursive".execute()
    proc.consumeProcessOutput(sout, serr)
    proc.waitFor()
    print sout
    print serr

    println("Done with pre-project setup")
}


build.dependsOn copyJar, copyServerJar
